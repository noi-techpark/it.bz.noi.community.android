// SPDX-FileCopyrightText: NOI Techpark <digital@noi.bz.it>
//
// SPDX-License-Identifier: CC0-1.0

plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'com.google.gms.google-services'
	id 'com.google.firebase.crashlytics'
	id 'kotlinx-serialization'
	id 'kotlin-parcelize'
	id 'androidx.navigation.safeargs'
}

android {

	namespace "it.bz.noi.community"
	compileSdk 34

	// Version Code - Start
	def Properties versionProps = new Properties()
	def versionPropsFile = file('version.properties')

	if (versionPropsFile.exists()) {
		versionProps.load(new FileInputStream(versionPropsFile))
	}

	def code = (versionProps['VERSION_CODE'] ?: "0").toInteger()
	// Version Code - End

	defaultConfig {
		applicationId "it.bz.noi.community"
		minSdkVersion 24
		targetSdkVersion 33
		versionCode code
		versionName "1.0.0"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		buildConfigField("String", "ISSUER_URL", "\"https://auth.opendatahub.com/auth/realms/noi/\"")
		buildConfigField("String", "SIGNUP_URL", "\"https://auth.opendatahub.com/auth/realms/noi/protocol/openid-connect/registrations?client_id=it.bz.noi.community&redirect_uri=https://noi.bz.it&response_type=code&scope=openid\"")
		buildConfigField("String", "COMMUNITY_API_URL", "\"https://api.community.noi.opendatahub.com\"")
		buildConfigField("String", "OPENDATAHUB_API_BASE_URL", "\"https://tourism.opendatahub.com/\"")
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			kotlinOptions {
				freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
			}
		}
		debug {
			kotlinOptions {
				freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
			}
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	kotlinOptions {
		jvmTarget = '1.8'
	}
	buildFeatures {
		viewBinding true
		buildConfig true
	}

	flavorDimensions += "environment"
	productFlavors {
		development {
			buildConfigField("String", "ISSUER_URL", "\"https://auth.opendatahub.testingmachine.eu/auth/realms/noi/\"")
			buildConfigField("String", "SIGNUP_URL", "\"https://auth.opendatahub.testingmachine.eu/auth/realms/noi/protocol/openid-connect/registrations?client_id=it.bz.noi.community&redirect_uri=https://noi.bz.it&response_type=code&scope=openid\"")
			buildConfigField("String", "COMMUNITY_API_URL", "\"https://api.community.noi.testingmachine.eu\"")
			dimension = 'environment'
		}
		production {
			buildConfigField("String", "ISSUER_URL", "\"https://auth.opendatahub.com/auth/realms/noi/\"")
			buildConfigField("String", "SIGNUP_URL", "\"https://auth.opendatahub.com/auth/realms/noi/protocol/openid-connect/registrations?client_id=it.bz.noi.community&redirect_uri=https://noi.bz.it&response_type=code&scope=openid\"")
			buildConfigField("String", "COMMUNITY_API_URL", "\"https://api.community.noi.opendatahub.com\"")
			dimension = 'environment'
		}
	}
}

dependencies {

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation 'androidx.core:core-ktx:1.12.0'
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'com.google.android.material:material:1.10.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
	implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
	implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
	testImplementation 'junit:junit:4.13.2'
	implementation 'androidx.recyclerview:recyclerview:1.3.2'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
	implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.1'

	// DataStore Preferences
	implementation("androidx.datastore:datastore-preferences:1.0.0")

	// Android LifeCycle
	def lifecycle_version = "2.6.2"
	implementation "androidx.lifecycle:lifecycle-common:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

	// Retrofit
	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
	implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'

	// Kotlin Coroutines
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

	// Glide
	implementation 'com.github.bumptech.glide:glide:4.16.0'
	annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'

	// SwipeRefresh Layout
	implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

	// Paging 3
	implementation "androidx.paging:paging-runtime-ktx:3.2.1"

	// BoM for the Firebase platform
	implementation platform('com.google.firebase:firebase-bom:32.6.0')
	// When using the BoM, you don't specify versions in Firebase library dependencies
	implementation 'com.google.firebase:firebase-crashlytics-ktx'
	implementation 'com.google.firebase:firebase-messaging-ktx'

	// OAuth
	implementation 'net.openid:appauth:0.11.1'
}
